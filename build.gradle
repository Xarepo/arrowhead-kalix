buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    group = 'eu.arrowhead.kalix'
    version = '0.1.0'

    tasks.withType(JavaCompile) {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }
}

configure([
        project(':kalix-base'),
        project(':kalix-core-plugins'),
        project(':kalix-processors')
]) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    afterEvaluate {
        java {
            withJavadocJar()
            withSourcesJar()
        }
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            mavenJava(MavenPublication) {
                pom {
                    url = 'https://arkalix.se'
                    licenses {
                        license {
                            name = 'The Eclipse Public License, Version 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0/'
                        }
                    }
                    developers {
                        developer {
                            id = 'emanuelpalm'
                            name = 'Emanuel Palm'
                            email = 'emanuel.palm@arkalix.se'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/emanuelpalm/arrowhead-kalix.git'
                        developerConnection = 'scm:git:ssh://github.com/emanuelpalm/arrowhead-kalix.git'
                        url = 'http://github.com/emanuelpalm/arrowhead-kalix/'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    afterEvaluate {
        javadoc {
            if (JavaVersion.current().isJava9Compatible()) {
                options.addBooleanOption('html5', true)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
